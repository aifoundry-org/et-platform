# Copyright (c) 2025 Ainekko, Co.
# SPDX-License-Identifier: Apache-2.0

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(etsoc_hal REQUIRED)
find_dependency(esperantoTrace REQUIRED)

set(_supported_components cm-umode minion-bl mm-rt-svcs cm-rt-svcs sp-bl1 sp-bl2)

# Find all components if user does not cherry-pick components
if (NOT et-common-libs_FIND_COMPONENTS)
    set(et-common-libs_FIND_COMPONENTS ${_supported_components})
endif()

# find components
foreach(_component ${et-common-libs_FIND_COMPONENTS})
    if (NOT ${_component} IN_LIST _supported_components)
        set(et-common-libs_FOUND False)
        set(et-common-libs_NOT_FOUND_MESSAGE "Unsupported component: ${_component}")
    endif()
    include(${CMAKE_CURRENT_LIST_DIR}/${_component}/${_component}Targets.cmake)
    set(et-common-libs_${_component}_FOUND True)
endforeach()

check_required_components(et-common-libs)
