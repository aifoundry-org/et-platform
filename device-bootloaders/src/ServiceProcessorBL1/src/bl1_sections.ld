/*-------------------------------------------------------------------------
* Copyright (c) 2025 Ainekko, Co.
* SPDX-License-Identifier: Apache-2.0
*-------------------------------------------------------------------------
*/

INCLUDE bl_layout.ld

ENTRY(bl1_entry)

MEMORY
{
    CODE (rx)  : org = BL1_CODE_REGION_BASE, l = BL1_CODE_REGION_SIZE
    DATA (!rx) : org = BL1_DATA_REGION_BASE, l = BL1_DATA_REGION_SIZE
}

SECTIONS
{
    .text : ALIGN(128)
    {
        __code_start = .;
        *(SORT_BY_ALIGNMENT(.text) SORT_BY_ALIGNMENT(.text.*) SORT_BY_ALIGNMENT(.gnu.linkonce.t.*))
    } > CODE

    .rodata :
    {
        *(SORT_BY_ALIGNMENT(.rodata) SORT_BY_ALIGNMENT(.rodata.*) SORT_BY_ALIGNMENT(.gnu.linkonce.r.*))
    } > CODE

    .rodata1 :
    {
        *(SORT_BY_ALIGNMENT(.rodata1))
    } > CODE

    .sdata2 :
    {
        *(SORT_BY_ALIGNMENT(.sdata2) SORT_BY_ALIGNMENT(.sdata2.*) SORT_BY_ALIGNMENT(.gnu.linkonce.s2.*))
    } > CODE

    .sbss2 :
    {
        *(SORT_BY_ALIGNMENT(.sbss2) SORT_BY_ALIGNMENT(.sbss2.*) SORT_BY_ALIGNMENT(.gnu.linkonce.sb2.*))
        . = ALIGN(4);
        __code_end = .;
    } > CODE

    .data : ALIGN(128)
    {
        /* ALIGN(128) above should ensure crt.S data init asm has 64-bit aligned start address */
        __data_start = .;

        *(SORT_BY_ALIGNMENT(.data) SORT_BY_ALIGNMENT(.data.*) SORT_BY_ALIGNMENT(.gnu.linkonce.d.*))
    } > DATA

    /* We want the small data sections together, so single-instruction offsets
      can access them all, and initialized data all before uninitialized, so
      we can shorten the on-disk segment size.  */
    .sdata :
    {
        /* the magic __global_pointer$ symbol is defined to point 0x800 bytes past the
          start of the .sdata section. The 0x800 magic number allows signed 12-bit
          offsets from __global_pointer$ to address symbols at the start of the .sdata
          section. The linker assumes that if this symbol is defined, then the gp
          register contains that value, which it can then use to relax accesses to
          global symbols within that 12-bit range.
          See https://www.sifive.com/blog/all-aboard-part-3-linker-relaxation-in-riscv-toolchain*/
        __global_pointer$ = . + 0x800;
        *(.srodata.cst16) *(.srodata.cst8) *(.srodata.cst4) *(.srodata.cst2) *(.srodata .srodata.*)
        *(SORT_BY_ALIGNMENT(.sdata) SORT_BY_ALIGNMENT(.sdata.*) SORT_BY_ALIGNMENT(.gnu.linkonce.s.*))

        /* Align here to ensure crt.S data init asm has 64-bit aligned end address */
        . = ALIGN(8);
        __data_end = .;
    } > DATA

    .bss (NOLOAD) : ALIGN(128)
    {
        /* ALIGN(128) above should ensure crt.S bss init asm has a 64-bit aligned start address */
        __bss_start = .;

        *(.bss .bss.* .gnu.linkonce.b.*)
    } > DATA

    .sbss (NOLOAD) :
    {
        *(.sbss .sbss.* .gnu.linkonce.sb.*)

        /* Align here to ensure crt.S bss init asm has a 64-bit aligned end address */
        . = ALIGN(8);
        __bss_end = .;
    } > DATA

    /* The default stack alignment is 16 bytes in RV32I and RV64I */
    . = ALIGN(16);

    stack (NOLOAD) :
    {
        KEEP(*(stack stack.*))
        __stack_base = .;
    } > DATA

    /DISCARD/ : { *(.riscv.attributes) }
}
